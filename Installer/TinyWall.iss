; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "TinyWall"
#define MyAppNameNoVersion "TinyWall"
#define MyAppVersion "1.0.2"
#define MyAppPublisher "Károly Pados"

#define TW_TinyWall_exe_SN "TinyWall, Version=1.0.2.0, Culture=neutral, PublicKeyToken=d9a8adbcd0c171b3"
#define TW_NetFwTypeLib_dll_SN "Interop.NetFwTypeLib, Version=1.0.0.0, Culture=neutral, PublicKeyToken=d9a8adbcd0c171b3"
#define TW_TransparentLabel_dll_SN "TransparentLabel, Version=1.0.0.0, Culture=neutral, PublicKeyToken=0b469a20c1a49c6a"
#define TW_XmlSerializers_dll_SN "TinyWall.XmlSerializers, Version=1.0.2.0, Culture=neutral, PublicKeyToken=d9a8adbcd0c171b3"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppID={{0686F23B-A0DD-45FD-A733-F70A11E8CE64}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
DefaultDirName={pf}\{#MyAppNameNoVersion}
DefaultGroupName={#MyAppNameNoVersion}
DisableProgramGroupPage=yes
OutputBaseFilename=TinyWallInstaller
Compression=lzma2/Ultra64
SolidCompression=true
ChangesAssociations=false
CreateAppDir=true
InternalCompressLevel=Normal
MinVersion=,6.0.6000
Uninstallable=true
AlwaysShowComponentsList=false
ArchitecturesAllowed=x64 x86
ArchitecturesInstallIn64BitMode=x64
TerminalServicesAware=false
AllowUNCPath=false
AppMutex=Global\TinyWallService
UninstallDisplayName=TinyWall
UninstallDisplayIcon={app}\TinyWall.exe
PrivilegesRequired=admin
ShowLanguageDialog=no
AppCopyright=Copyright (c) 2011 {#MyAppPublisher}
AppVerName={#MyAppVersion}
AllowCancelDuringInstall=false
LicenseFile=Components/License.txt

[Messages]
SetupAppRunningError=Setup has detected that %1 is currently running.%n%nPlease start the uninstaller from the tray application to uninstall.

[Languages]
Name: "english"; MessagesFile: "compiler:Default.isl"

[Files]
Source: "Components\License.txt"; DestDir: "{app}"
Source: "Components\FAQ.pdf"; DestDir: "{app}"
Source: "Components\Attributions.txt"; DestDir: "{app}"
Source: "Components\TinyWall.exe.config"; DestDir: "{app}"; Flags:
Source: "Components\CommonAppData\*"; DestDir: "{commonappdata}"; Flags: recursesubdirs createallsubdirs 

Source: "Components\TinyWall.exe"; DestDir: "{app}"; Flags: replacesameversion gacinstall; StrongAssemblyName: "{#TW_TinyWall_exe_SN}";
Source: "Components\TransparentLabel.dll"; DestDir: "{app}"; Flags: replacesameversion gacinstall sharedfile uninsnosharedfileprompt; StrongAssemblyName: "{#TW_TransparentLabel_dll_SN}";
Source: "Components\Interop.NetFwTypeLib.dll"; DestDir: "{app}"; Flags: replacesameversion gacinstall sharedfile uninsnosharedfileprompt; StrongAssemblyName: "{#TW_NetFwTypeLib_dll_SN}";
Source: "Components\TinyWall.XmlSerializers.dll"; DestDir: "{app}"; Flags: replacesameversion gacinstall sharedfile uninsnosharedfileprompt; StrongAssemblyName: "{#TW_XmlSerializers_dll_SN}";

;Source: "Components\TinyWall.exe"; DestDir: "{app}"; Flags: replacesameversion; StrongAssemblyName: "{#TW_TinyWall_exe_SN}";
;Source: "Components\TransparentLabel.dll"; DestDir: "{app}"; Flags: replacesameversion sharedfile uninsnosharedfileprompt; StrongAssemblyName: "{#TW_TransparentLabel_dll_SN}";
;Source: "Components\Interop.NetFwTypeLib.dll"; DestDir: "{app}"; Flags: replacesameversion sharedfile uninsnosharedfileprompt; StrongAssemblyName: "{#TW_NetFwTypeLib_dll_SN}";
;Source: "Components\TinyWall.XmlSerializers.dll"; DestDir: "{app}"; Flags: replacesameversion sharedfile uninsnosharedfileprompt; StrongAssemblyName: "{#TW_XmlSerializers_dll_SN}";

[Run]
Filename: "{dotnet20}\ngen.exe"; Parameters: "install ""{#TW_TinyWall_exe_SN}"""; StatusMsg: "Optimizing performance..."; Flags: runascurrentuser runhidden;
Filename: "{app}\TinyWall.exe"; Parameters: "/install"; StatusMsg: "Installing service..."; Flags: runascurrentuser;
Filename: "{app}\TinyWall.exe"; Parameters: "/desktop"; StatusMsg: "Starting firewall..."; Flags: runasoriginaluser nowait; Tasks: not detectnow;
Filename: "{app}\TinyWall.exe"; Parameters: "/desktop /detectnow"; StatusMsg: "Starting firewall..."; Flags: runasoriginaluser nowait; Tasks: detectnow;

[Tasks]
Name: detectnow; Description: "Detect installed applications after setup to ease whitelisting"; Flags: checkedonce;

[UninstallRun]
Filename: "{app}\TinyWall.exe"; Parameters: "/uninstall"; StatusMsg: "Uninstalling service..."; Flags: runascurrentuser;
Filename: "{dotnet20}\ngen.exe"; Parameters: "uninstall ""{#TW_TinyWall_exe_SN}"""; Flags: runascurrentuser runhidden;

[UninstallDelete]
Type: filesandordirs; Name: "{app}"
Type: filesandordirs; Name: "{commonappdata}\TinyWall"

[Icons]
Name: "{group}\TinyWall Controller"; Filename: "{app}\TinyWall.exe"; Parameters: "/desktop"; IconFilename: {app}\TinyWall.exe; Flags: excludefromshowinnewinstall; Comment: "TinyWall Controller"; WorkingDir: "{app}";
;Name: "{group}\DevelTool"; Filename: "{app}\TinyWall.exe"; Parameters: "/develtool"; IconFilename: {app}\TinyWall.exe; Flags: excludefromshowinnewinstall; Comment: "TinyWall Development Helper"; WorkingDir: "{app}";
;Name: "{group}\Visit Website"; Filename: "{app}\website.url";
Name: "{group}\FAQ"; Filename: "{app}\FAQ.pdf"; Flags: excludefromshowinnewinstall;
Name: "{group}\Visit Website"; Filename: "http://tinywall.pados.hu"; Flags: excludefromshowinnewinstall;

[CustomMessages]
dotnetmissing=This application needs Microsoft .Net 3.5 which is not yet installed on this computer. Would you like to download it now?
CustomForm_Caption=Important notice
CustomForm_Description=Please read the following carefully to avoid confusion
CustomForm_Label1_Caption0= \
1. To uninstall TinyWall, you must use the Uninstall command from the tray %n \
   application (in Manage, Maintenance). The Windows Control Panel cannot %n \
   be used for uninstallation. %n \
%n \
2. After installation finishes, TinyWall prevents network access for most programs.%n \
   This means in general you won't be able to access the internet. To grant specific %n \
   applications internet access, use the "Whitelist by..." options from the tray menu.

[Code]
Var
Page: TWizardPage;
Label1: TLabel;
    
function InitializeSetup(): Boolean;
var
    ErrorCode: Integer;
    netFramework35Installed : Boolean;
    isInstalled: Cardinal;
begin
  result := true;

    // Check for the .Net 3.5 framework
  isInstalled := 0;
  netFramework35Installed := RegQueryDWordValue(HKLM, 'SOFTWARE\Microsoft\NET Framework Setup\NDP\v3.5', 'Install', isInstalled);
  if ((netFramework35Installed)  and (isInstalled <> 1)) then netFramework35Installed := false;
 
  if (netFramework35Installed = false) then
  begin
    if (MsgBox(ExpandConstant('{cm:dotnetmissing}'),
        mbConfirmation, MB_YESNO) = idYes) then
    begin
      ShellExec('open',
      'http://www.microsoft.com/download/en/details.aspx?id=22',
      '','',SW_SHOWNORMAL,ewNoWait,ErrorCode);
    end;
    result := false;
  end;
 
end;

procedure InitializeWizard;
begin
  Page := CreateCustomPage( wpLicense, ExpandConstant('{cm:CustomForm_Caption}'), ExpandConstant('{cm:CustomForm_Description}') );
  { Label1 } Label1 := TLabel.Create(Page);
   with Label1 do
   begin 
    Parent := Page.Surface;
    Caption := ExpandConstant('{cm:CustomForm_Label1_Caption0}');
    Left := ScaleX(16);
    Top := ScaleY(24);
    Width := ScaleX(400);
    Height := ScaleY(400);
    Font.Color := clRed;
   end;
end;
